plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id 'groovy'
    id "maven-publish"
}

group = "org.rundeck.plugins"
archivesBaseName = "object-store-plugin"

ext.pluginClassNames='org.rundeck.plugin.objectstore.ObjectStorePlugin'
ext.pluginName = 'Object Store Plugin'
ext.pluginDescription = 'Stores data in an Amazon S3 compliant object store'

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}

scmVersion{
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

version=scmVersion.version

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

dependencies {
    // Use the latest Groovy version for building this library
    implementation("org.rundeck:rundeck-core:${rundeckVersion}")
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    pluginLibs("io.minio:minio:7.1.0") {
        exclude(group: 'com.fasterxml.jackson.core')
    }

    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:3.11.0"
    testImplementation "org.testcontainers:testcontainers:1.16.0"
}

repositories {
    mavenLocal()
    mavenCentral()
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': '1.2'
        attributes 'Rundeck-Plugin-Archive': true
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
tasks.withType(Test) {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pdhw822/rdp-object-store-plugin")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}